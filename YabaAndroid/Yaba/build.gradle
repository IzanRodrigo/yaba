buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

configurations {
    apt
}

dependencies {

    compile project(':Volley')
    compile project(':PagerSlidingTabStrip')

    //Android
    compile 'com.android.support:appcompat-v7:18.0.0'

    //Jake Wharton
    apt 'com.jakewharton:butterknife:3.0.0'
    compile 'com.jakewharton:butterknife:3.0.0'
    compile 'com.jakewharton.timber:timber:1.1.0'

    //Square
    apt 'com.squareup.dagger:dagger-compiler:1.1.0'
    compile 'com.squareup.dagger:dagger:1.1.0'
    compile 'com.squareup:otto:1.3.4'

    //Parsing
    compile 'com.google.code.gson:gson:2.2.4'
    compile('com.thoughtworks.xstream:xstream:1.4.5') {
        //It's in the android jar
        exclude module: 'xmlpull'
    }
}

android {
    compileSdkVersion 18
    buildToolsVersion "18.1.0"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 18
    }

    final Console console = System.console();
    signingConfigs {
        if (console != null)
            release {
                storeFile file("yabakeystore.jks")
                storePassword console.readLine("\nEnter keystore password: ")
                keyAlias 'Yaba'
                keyPassword console.readLine("Enter key password: ")
            }
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"
            versionNameSuffix ".d"
        }
        if (console != null)
            release {
                signingConfig signingConfigs.release
            }
    }
}

android.applicationVariants.all { variant ->
    android.ext.aptOut = file("${project.buildDir}/source/apt/${variant.dirName}")

    variant.javaCompile.doFirst {
        android.ext.aptOut.mkdirs()
        variant.javaCompile.options.compilerArgs += [
                '-processorpath', configurations.apt.getAsPath(),
                '-s', android.ext.aptOut
        ]
    }
}